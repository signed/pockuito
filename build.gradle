apply plugin: "java"
apply plugin: "idea"

idea {
    project {
        ipr {
            withXml {
                def project = it.asNode()
                def vcsDirectoryMappings = project.component.find { it.@name == 'VcsDirectoryMappings' };
                vcsDirectoryMappings.children().clear()
                vcsDirectoryMappings.appendNode('mapping', ['directory': '$PROJECT_DIR$', 'vcs': 'Git'])
            }
        }
    }
}

repositories{
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.mockito:mockito-core:1.9.5'
    testCompile 'junit:junit-dep:4.10'
    testCompile 'info.cukes:cucumber-java:1.1.1'
    testCompile 'info.cukes:cucumber-junit:1.1.1'
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.signed.pockuito'
version = '0.0.2-SNAPSHOT'

//def repositoryUrl = 'http://localhost:8081/nexu/content/repositories/snapshots'
def repositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
if(project.hasProperty('release')){
    project.version -= '-SNAPSHOT'
    //repositoryUrl = 'http://localhost:8081/nexus/content/repositories/releases'
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

//http://jedicoder.blogspot.de/2011/11/automated-gradle-project-deployment-to.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: repositoryUrl) {
                authentication(userName: deploymentNexusLocalLogin, password: deploymentNexusLocalPassword)
            }

            pom.project {
                name 'pockutio'
                packaging 'jar'
                inceptionYear '2012'
                description 'pockutio is a set of small extensions to mockito'
                url 'https://github.com/signed/pockuito'

                scm {
                    url 'scm:git@github.com:signed/pockuito.git'
                    connection 'scm:git://github.com/signed/pockuito.git'
                    developerConnection 'scm:git@github.com:signed/pockuito.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://raw.github.com/signed/pockuito/master/license'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'thomasheilbronner'
                        name 'Thomas Heilbronner'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
